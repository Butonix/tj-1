buildscript {
    ext.kotlin_version = '1.0.0'

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    signingConfigs {
        if (project.hasProperty('tjr.signing.keyAlias')) {
            prod {
                keyAlias project['tjr.signing.keyAlias']
                keyPassword project['tjr.signing.keyPassword']
                storeFile file(project['tjr.signing.storeFile'])
                storePassword project['tjr.signing.storePassword']
            }
        }
    }

    defaultConfig {
        applicationId "com.a6v.tjreader"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 2
        versionName "1.0"
        if (project.hasProperty('tjr.signing.keyAlias')) {
            signingConfig signingConfigs.prod
        }
        buildConfigField "boolean", "LOG_ENABLED", "false"
    }
    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            buildConfigField "boolean", "LOG_ENABLED", "true"
        }
        release {
            minifyEnabled false
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    lintOptions {
        abortOnError false
    }
}

kapt {
    generateStubs = true
}

def supportVersion = '23.1.1'
def daggerVersion = '2.0.2'
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'org.jetbrains.anko:anko-sqlite:0.8.2'

    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"

    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    compile "com.google.dagger:dagger:$daggerVersion"
    provided 'org.glassfish:javax.annotation:10.0-b28'

    compile 'io.reactivex:rxjava:1.1.2'
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'com.trello:rxlifecycle:0.5.0'
    compile 'com.trello:rxlifecycle-components:0.5.0'
    compile 'io.reactivex:rxkotlin:0.40.1'

    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.7.5'
    compile 'com.squareup.okhttp:logging-interceptor:2.7.5'

    //compile 'nl.qbusict:cupboard:2.1.4'
    //compile 'com.pushtorefresh.storio:sqlite:1.8.0'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    compile 'org.jsoup:jsoup:1.8.3'

    testCompile 'junit:junit:4.12'
}
